# vim: syntax=python

rule phasing_tablefy:
    input:
        bcf=config['phaser'] + '/phased_filtered.bcf',
        bcfidx=config['phaser'] + '/phased_filtered.bcf.csi'
    output:
        tab='call_mutations/phase_info.tab.gz',
        tabidx='call_mutations/phase_info.tab.gz.tbi',
    log:
        'call_mutations/phase_info.log'
    benchmark:
        'call_mutations/phase_info.benchmark.txt'
    resources:
        mem_mb=250
    shell:
        """
        (echo "#chr pos refnt altnt phasedgt" | tr ' ' '\t' ; 
         bcftools query -f "%CHROM\t%POS\t%REF\t%ALT[\t%GT]\n" {input.bcf}) \
        | bgzip -c > {output.tab}
        tabix -pvcf -S 1 {output.tab}
        """


rule phasing_gather_file_list:
    input:
        vcfs=expand("{phaser}/{chr}/phased.vcf",
            phaser=config['phaser'],
            chr=config['chrs'])
    output:
        filelist=temp("{phaser}/file_list.txt")
    localrule: True
    run:
        with open(output.filelist, 'w') as f:
            for vcf in input.vcfs:
                f.write(str(vcf) + "\n")


rule phasing_gather:
    input:
        filelist=config['phaser'] + "/file_list.txt",
        vcfs=expand("{phaser}/{chr}/phased.vcf",
            phaser=config['phaser'],
            chr=config['chrs'])
    output:
        bcf=temp(config['phaser'] + "/phased_filtered.bcf"),
        bcfidx=temp(config['phaser'] + "/phased_filtered.bcf.csi"),
    benchmark:
        config['phaser'] + "/phased_filtered.benchmark.txt"
    resources:
        mem_mb=250
    shell:
        """
        bcftools concat \
            --file-list {input.filelist} \
            -Ob -o {output.bcf}
        bcftools index -o {output.bcfidx} {output.bcf}
        """


# Step 1 - get just the bulk sample with non-ref alleles to simplify
# next steps.
rule phasing_prepare_bulk:
    input:
        bcf="call_mutations/analyzable_sites.mmq60.bcf",
        bcfidx="call_mutations/analyzable_sites.mmq60.bcf.csi"
    output:
        bcf=config['phaser'] + "/analyzable_sites.mmq60.filtered_bulk_{muttype}_only.bcf",
        bcfidx=config['phaser'] + "/analyzable_sites.mmq60.filtered_bulk_{muttype}_only.bcf.csi"
    log:
        config['phaser'] + "/analyzable_sites.mmq60.filtered_bulk_{muttype}_only.log",
    benchmark:
        config['phaser'] + "/analyzable_sites.mmq60.filtered_bulk_{muttype}_only.benchmark.txt",
    params:
        # snv -> snp
        muttype=lambda wildcards: 'snp' if wildcards.muttype == 'snv' else 'indel',
        bulk_sample=config['bulk_sample'],
        min_dp=lambda wildcards: config[wildcards.muttype + '_min_bulk_dp']
    resources:
        mem_mb=250
    shell:
        """
        bcftools view \
            -Ou \
            --samples {params.bulk_sample} \
            --trim-alt-alleles \
            --include 'TYPE="{params.muttype}"' \
            {input.bcf} \
        | bcftools annotate \
            -Ou \
            --remove '^FORMAT/GT,^FORMAT/AD,^FORMAT/DP,^FORMAT/GQ,INFO' \
        | bcftools view \
            --include 'ALT != "." & FORMAT/GT != "mis" & FORMAT/GT != "ref" & FORMAT/DP >= {params.min_dp}' \
            -Ob -o {output.bcf}
        bcftools index -o {output.bcfidx} {output.bcf}
        """


# Very, very simple filtering to remove the most egregious bulk genotypes.
#
# It is important to remove very bad genotypes because SHAPEIT and Eagle
# take them literally -- e.g., a depth=30 site with 1 reference read and
# 29 variant reads in bulk can sometimes flagged as genotype=0/1 in GATK's
# raw output. This is almost certainly wrong, and may confuse the phaser
# since it takes the 0/1 assignment at face value.
rule phasing_prepare_filter_by_qual:
    input:
        bcf=config['phaser'] + "/analyzable_sites.mmq60.filtered_bulk_{muttype}_only.bcf",
        bcfidx=config['phaser'] + "/analyzable_sites.mmq60.filtered_bulk_{muttype}_only.bcf.csi"
    output:
        qual_cutoff=config['phaser'] + "/{chr}/qual_cutoff_{muttype}.txt",
        vcf=temp(config['phaser'] + "/{chr}/analyzable_sites.mmq60.filtered_bulk_{muttype}_only.qual_cutoff.vcf")
    log:
        config['phaser'] + "/{chr}/analyzable_sites.mmq60.filtered_bulk_{muttype}_only.qual_cutoff.log",
    benchmark:
        config['phaser'] + "/{chr}/analyzable_sites.mmq60.filtered_bulk_{muttype}_only.qual_cutoff.benchmark.txt"
    params:
        chrom='{chr}',
        phasing_qual_cutoff_quantile=config['phasing_qual_cutoff_quantile'],
        phasing_gq_cutoff=30
    localrule: True
    resources:
        mem_mb=250
    shell:
        # bcftools query+Rscript calculates the QUAL score cutoff
        # bcftools filter removes sites that don't meet the QUAL cutoff
        #
        # N.B. it looks like QUAL is better at removing poor calls than FORMAT/GQ.
        # Initially I thought the opposite may be true, because FORMAT/GQ is
        # specific to the sample while QUAL represents all samples at the locus,
        # making QUAL less appropriate a measure of the bulk sample.
        # 
        # However, it seems that the same reason has the opposite effect:
        # aggregating information across the bulk and single cells is an
        # information gain. This makes perfect sense for germline variants, since
        # the majority of these should be shared by all single cells as well.
        """
        bcftools query \
            -r {params.chrom} \
            -f '%QUAL\n' {input.bcf} \
        | Rscript -e 'x <- scan("stdin", what=0, quiet=TRUE); cat(quantile(x, probs={params.phasing_qual_cutoff_quantile}))' > {output.qual_cutoff}
        bcftools filter \
            -r {params.chrom} \
            -Ov -o {output.vcf} \
            --include "FORMAT/GQ >= {params.phasing_gq_cutoff} & QUAL >= $(cat {output.qual_cutoff})" \
            {input.bcf}
        """


# Handling for haploid chromosomes
rule phase_chrY:
    input:
        vcf="shapeit/" + config['chrY'] + "/final_for_phasing.analyzable_sites.mmq60.vcf"
    output:
        vcf=temp("shapeit/" + config['chrY'] + "/phased.vcf")
    log:
        "shapeit/" + config['chrY'] + "/shapeit.log"
    benchmark:
        "shapeit/" + config['chrY'] + "/benchmark_phaser.tsv"
    shell:
        # bcftools annotate trims down the format field to only the GT tag,
        # to match shapeit's output.
        # Then setGT sets every genotype to phased, under the assumption that
        # bulk GTs will be primarily hom. alt (1/1).
        # It turns out this is a poor assumption at least for human chrY. Even
        # after excluding PARs, there were ~3-fold more het genotypes in bulk
        # then hom genotypes (1633 0/1 vs. 687 1/1), suggesting that variant
        # calling on chrY in general is very poor.
        """
        bcftools annotate \
            --remove '^FORMAT/GT' \
            {input.vcf} \
        | bcftools +setGT \
            -Ov -o {output} \
            -- \
            --target-gt a \
            --new-gt p
        """
