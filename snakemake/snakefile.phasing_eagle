# vim: syntax=python

# almost identical to shapeit_prepare - the only difference is the final
# output is .vcf.gz instead of .vcf
rule eagle_prepare:
    input:
        snv_vcf="eagle/{chr}/analyzable_sites.mmq60.filtered_bulk_snv_only.qual_cutoff.vcf",
        indel_vcf="eagle/{chr}/analyzable_sites.mmq60.filtered_bulk_indel_only.qual_cutoff.vcf",
    output:
        vcfgz="eagle/{chr}/final_for_phasing.analyzable_sites.mmq60.vcf.gz",
        vcfgzi="eagle/{chr}/final_for_phasing.analyzable_sites.mmq60.vcf.gz.tbi"
    log:
        "eagle/{chr}/final_for_phasing.analyzable_sites.mmq60.log"
    benchmark:
        "eagle/{chr}/final_for_phasing.analyzable_sites.mmq60.benchmark.txt"
    localrule: True
    resources:
        mem_mb=250
    shell:
        """
        (bcftools concat \
             {input.snv_vcf} {input.indel_vcf} \
         | bcftools sort --max-mem={resources.mem_mb}M \
         | bcftools norm \
             --multiallelics +both \
             --do-not-normalize \
             -Oz -o {output.vcfgz}) >& {log}
        tabix -pvcf {output.vcfgz}
        """


ruleorder: phase_chrY > eagle_scatter

rule eagle_scatter:
    input:
        vcfgz="eagle/{chr}/final_for_phasing.analyzable_sites.mmq60.vcf.gz"
    output:
        vcf=temp("eagle/{chr}/phased.vcf")
    log:
        "eagle/{chr}/eagle.log"
    benchmark:
        "eagle/{chr}/benchmark_phaser.tsv"
    params:
        refbcf=lambda wildcards: config['eagle_refpanel'][wildcards.chr],
        outprefix="eagle/{chr}/phased"
    resources:
        mem_mb=8000
    shell:
        """
        eagle --vcfTarget {input.vcfgz} \
            --vcfRef {params.refbcf} \
            --geneticMapFile {config[eagle_genmap]} \
            --vcfOutFormat v \
            --outPrefix {params.outprefix}
        """
