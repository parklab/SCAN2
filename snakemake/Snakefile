# vim: syntax=python

wildcard_constraints:
    chr='|'.join(str(x) for x in config['chrs']),
    gatk_chunk="\d+",
    gatk_mmq="\d+",
    phaser="shapeit|eagle",
    depth_method="gatkdocov|samtoolsdepth",
    muttype='snv|indel',
    passtype='pass|rescue'

print("SCAN2 version=" + config['scan2_version'] + ", buildnum=" + str(config['scan2_buildnum']) + ", githash=" + config['scan2_githash'])

if config['analysis'] == 'call_mutations':
    include: "snakefile.call_mutations"

    rule call_mutations:
        input:
            "call_mutations_collected_benchmarks.txt",
            objects=lambda wildcards: expand("call_mutations/{sample}/scan2_object.rda",
                sample=config['sc_bams'].keys())
        default_target: True

if config['analysis'] == 'makepanel':
    include: "snakefile.makepanel"
    rule makepanel:
        input:
            "panel/panel.tab.gz",
            "makepanel_collected_benchmarks.txt"

if config['analysis'] == 'rescue':
    include: "snakefile.rescue"
    rule rescue_batch:
        input:
            "rescued_muts.txt",
            "sig_homogeneity_tests.txt",
            expand("objects/{sample}_scan2_object_rescue.rda",
                sample=config['scan2_objects'].keys())

if config['analysis'] == 'permtool':
    include: "snakefile.permtool"
    # Allow user to skip the final combine stage. In this mode, the user just
    # wants access to all of the individual permutation objects so they can
    # combine them in arbitrary ways.
    if config['no_combine_permutations']:
        rule permtool:
            input:
                expand("perms_by_sample/{sample}/{muttype}_{passtype}.rda",
                    sample=config['permtool_config_map'].keys(),
                    muttype=[ 'snv', 'indel' ], passtype=[ 'pass', 'rescue' ])
    else:
        rule permtool:
            input:
                expand("perms_{muttype}_{passtype}.rda",
                    muttype=[ 'snv', 'indel' ], passtype=[ 'pass', 'rescue' ]),
                expand("seedinfo_{muttype}_{passtype}.rda",
                    muttype=[ 'snv', 'indel' ], passtype=[ 'pass', 'rescue' ])
